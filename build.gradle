plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.17.0"
}

version '2.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'com.heroku.sdk:heroku-deploy:3.0.4'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

gradlePlugin {
    plugins {
        herokuGradle {
            id = 'com.heroku.sdk.heroku-gradle'
            implementationClass = 'com.heroku.sdk.HerokuGradlePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/heroku/heroku-gradle'
    vcsUrl = 'https://github.com/heroku/heroku-gradle.git'
    description = 'A Gradle plugin for deploying to Heroku.'
    tags = ['heroku', 'deployment']

    plugins {
        herokuGradle {
            displayName = 'Heroku Gradle'
        }
    }
}

// To determine the plugin version at runtime, we package a properties file with the current version string:
task writeVersionPropertiesFile(type: WriteProperties) {
    outputFile = file('src/main/resources/heroku-gradle.properties')
    property 'version', project.version
}

assemble.dependsOn writeVersionPropertiesFile
